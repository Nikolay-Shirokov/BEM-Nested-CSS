Перем ИмяФайлаСтилей;
Перем ИмяФайлаРазметки;
Перем КаталогСБлоками;
Перем КаталогССтилямиСтраниц;
Перем СтрокаЗаменыПереносаСтроки;

Процедура ПрочитатьПараметрыКоманднойСтроки()

  Если АргументыКоманднойСтроки.Количество() > 0 Тогда
    ИмяФайлаСтилей = АргументыКоманднойСтроки[0];
  КонецЕсли;

  Если АргументыКоманднойСтроки.Количество() > 1 Тогда
    ИмяФайлаРазметки = АргументыКоманднойСтроки[1];
    ИмяФайлаРазметки = СтрЗаменить(ИмяФайлаРазметки, ".html", "");
  КонецЕсли;

КонецПроцедуры

Функция КаталогСуществует(ИмяКаталога)
  Возврат Новый Файл(ИмяКаталога).Существует();
КонецФункции

Функция ПрочитатьФайлСтилей()

  ТекстовыйДокумент = Новый ТекстовыйДокумент();
  ТекстовыйДокумент.Прочитать(ИмяФайлаСтилей);

  Текст = ТекстовыйДокумент.ПолучитьТекст();
  ТекстовыйДокумент = Неопределено;

  РегулярноеВыражение = Новый РегулярноеВыражение("^\.(.*?)[\s|\:].*?\{.*?\}");
  РегулярноеВыражение.Многострочный = Истина;

  Текст = СтрЗаменить(Текст, Символы.ПС, СтрокаЗаменыПереносаСтроки);
  Текст = СтрЗаменить(Текст, СтрокаЗаменыПереносаСтроки+".", Символы.ПС + ".");
  Совпадения = РегулярноеВыражение.НайтиСовпадения(Текст);

  Классы        = Новый Массив;
  СтилиКлассов  = Новый Соответствие;

  Для каждого Совпадение Из Совпадения Цикл

    Класс = Совпадение.Группы[1].Значение;
    СтильКласса = СтрЗаменить(Совпадение.Значение, СтрокаЗаменыПереносаСтроки, Символы.ПС);
    СтильКласса = СтрЗаменить(СтильКласса, "../", "/");

    СтилиКласса = СтилиКлассов[Класс];
    Если СтилиКласса = Неопределено Тогда
      СтилиКласса = Новый Массив;
      Классы.Добавить(Класс);
    КонецЕсли;

    СтилиКласса.Добавить(СтильКласса);
    СтилиКлассов.Вставить(Класс, СтилиКласса);

  КонецЦикла;

  Возврат Новый Структура("Классы, СтилиКлассов", Классы, СтилиКлассов);

КонецФункции

Функция ОпределитьПутьККаталогуКласса(Класс, СообщитьОбрабатываемыйКласс = Ложь)

  СоставляющиеКласса = СтрРазделить(Класс, "_");
  КоличествоСоставляющих = СоставляющиеКласса.Количество();

  Блок = СоставляющиеКласса[0];
  Если КоличествоСоставляющих = 1 Тогда
    ВидКласса = "Блок";
  ИначеЕсли ПустаяСтрока(СоставляющиеКласса[1]) Тогда
    Если КоличествоСоставляющих > 3 Тогда
      ВидКласса = "ЭлементМодификатор";
    Иначе
      ВидКласса = "Элемент";
    КонецЕсли;
  Иначе
    ВидКласса = "БлокМодификатор";
  КонецЕсли;

  Если СообщитьОбрабатываемыйКласс Тогда
    Сообщить(СтрШаблон("%1 (%2)", Класс, ВидКласса));
  КонецЕсли;

  Если ВидКласса = "Блок" Тогда
    ПутьККаталогу = ОбъединитьПути(КаталогСБлоками, Блок);
  ИначеЕсли ВидКласса = "БлокМодификатор" Тогда
    ПутьККаталогу = ОбъединитьПути(КаталогСБлоками
        , Блок
        , СтрШаблон("_%1", СоставляющиеКласса[1])
      );
  ИначеЕсли ВидКласса = "Элемент" Тогда
    ПутьККаталогу = ОбъединитьПути(КаталогСБлоками
        , Блок
        , СтрШаблон("__%1", СоставляющиеКласса[2])
      );
  ИначеЕсли ВидКласса = "ЭлементМодификатор" Тогда
    ПутьККаталогу = ОбъединитьПути(КаталогСБлоками
        , Блок
        , СтрШаблон("__%1", СоставляющиеКласса[2])
        , СтрШаблон("_%1", СоставляющиеКласса[3])
      );
  Иначе

  КонецЕсли;

  Возврат ПутьККаталогу;

КонецФункции

Процедура СоздатьЗаполнитьФайлыКлассов(СтруктураКлассов)

  СтрокаСоединения = Символы.ПС + Символы.ПС;

  Для каждого Класс Из СтруктураКлассов.Классы Цикл

    ПутьККаталогу = ОпределитьПутьККаталогуКласса(Класс, Истина);

    Если Не КаталогСуществует(ПутьККаталогу) Тогда
      СоздатьКаталог(ПутьККаталогу);
    КонецЕсли;

    СтилиКласса = СтруктураКлассов.СтилиКлассов[Класс];
    ТекстСтилей = СтрСоединить(СтилиКласса, СтрокаСоединения);

    ТекстовыйДокумент = Новый ТекстовыйДокумент;
    ТекстовыйДокумент.УстановитьТекст(ТекстСтилей);
    ТекстовыйДокумент.Записать(ОбъединитьПути(ПутьККаталогу, СтрШаблон("%1.css", Класс)), КодировкаТекста.UTF8);

  КонецЦикла;

КонецПроцедуры

Процедура СоздатьФайлСтилейСтраницы(КоллекцияКлассов)

  Если Не КаталогСуществует(КаталогССтилямиСтраниц) Тогда
    СоздатьКаталог(КаталогССтилямиСтраниц);
  КонецЕсли;

  ТекстовыйДокумент = Новый ТекстовыйДокумент();

  Для каждого Класс Из КоллекцияКлассов Цикл

    ПутьККаталогу = ОпределитьПутьККаталогуКласса(Класс);
    ОтносительныйПутьКФайлуСтилейКласса = ОбъединитьПути(ПутьККаталогу, СтрШаблон("%1.css", Класс));
    ОтносительныйПутьКФайлуСтилейКласса = СтрЗаменить(ОтносительныйПутьКФайлуСтилейКласса, "\", "/");
    СтрокаИмпорта = СтрШаблон("@import url(../%1);", ОтносительныйПутьКФайлуСтилейКласса);

    ТекстовыйДокумент.ДобавитьСтроку(СтрокаИмпорта);

  КонецЦикла;

  ТекИмяФайлаСтилей = ОбъединитьПути(КаталогССтилямиСтраниц, СтрШаблон("%1.css", ИмяФайлаРазметки));

  ТекстовыйДокумент.Записать(ТекИмяФайлаСтилей, КодировкаТекста.UTF8);

КонецПроцедуры

ИмяФайлаСтилей          = "./styles/style.css";
КаталогСБлоками         = "blocks";
КаталогССтилямиСтраниц  = "pages";
ИмяФайлаРазметки        = "index";

СтрокаЗаменыПереносаСтроки = "#PS101#";

ПрочитатьПараметрыКоманднойСтроки();

ФайлСтилей = ПрочитатьФайлСтилей();
СоздатьЗаполнитьФайлыКлассов(ФайлСтилей);
СоздатьФайлСтилейСтраницы(ФайлСтилей.Классы);



